buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.google.protobuf'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

version = '0.1.2'
group = 'org.retrostore'

dependencies {
    compile 'com.google.guava:guava:20.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.protobuf:protobuf-lite:3.0.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    // This will generate the protobuf files into the regular source tree.
    // I wasn't able to keep them separate (e.g. in project/gen) and then
    // have IntelliJ still play nicely. Would love to revisit this, if
    // anybody is reading this and has an idea. But for now, this works
    // pretty nicely.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {
                    outputSubDir = "java"
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}
clean {
    delete "$projectDir/src/main/java/org/retrostore/client/common/proto"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Retrostore Client Common Library',
                'Implementation-Version': version
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/tmp/myRepo/")
            pom {
                artifactId = "retrostore-client-common"
            }
        }
    }
}